import org.apache.tools.ant.filters.ReplaceTokens
import groovy.json.JsonSlurper

group 'io.paul1365972.github'
version 'dev-SNAPSHOT'

task paperPaperclip(type: Exec, group: 'paper') {
    commandLine "$BASH_PATH", 'ilblu', 'jar'
}

task paperApplyPatches(type: Exec, group: 'paper') {
    commandLine "$BASH_PATH", 'ilblu', 'patch'
}

task paperApplyPatchesFast(type: Exec, group: 'paper') {
    commandLine "$BASH_PATH", 'ilblu', 'patch', 'fast'
}

task paperBuild(type: Exec, group: 'paper') {
    commandLine "$BASH_PATH", 'ilblu', 'build'
}

task paperRebuildPatches(type: Exec, group: 'paper') {
    commandLine "$BASH_PATH", 'ilblu', 'rebuild'
}

task paperUpstream(type: Exec, group: 'paper') {
    commandLine "$BASH_PATH", 'ilblu', 'upstream'
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    // TODO propertyfy
    group 'io.github.paul1365972'
    version '1.15.2-R0.1-SNAPSHOT'

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
    }

    processResources {
        filter ReplaceTokens, tokens: [FORK_NAME: FORK_NAME]
        into("META-INF/maven/$project.group/${project.name.toLowerCase()}") {
            from { generatePomFileForMavenPublication }
            rename ".*", "pom.xml"
        }
        into("META-INF/maven/$project.group/${project.name.toLowerCase()}") {
            from { generatePomProperties }
        }
    }

    task generatePomProperties(type: WriteProperties) {
        outputFile file("$buildDir/generated/pom.properties")
        comment 'Generated by Gradle'
        property 'artifactId', project.name.toLowerCase()
        property 'groupId', project.group
        property 'version', project.version
    }
}

allprojects {
    // TODO Find fix to not rely on rootProject as it may change
    task parseBuildData() {
        ext.mcver = ""
        def buildDataInfo = rootProject.file("./$WORK_PATH/BuildData/info.json")
        inputs.file buildDataInfo
        doLast {
            ext.mcver = new JsonSlurper().parse(buildDataInfo)['minecraftVersion']
        }
    }

    task gitDescribeFork(type: Exec) {
        commandLine "$GIT_PATH", 'rev-parse', '--short', '--always', 'HEAD'
        workingDir rootProject.projectDir
        standardOutput = new ByteArrayOutputStream()
        ext.output = { return standardOutput.toString().replaceAll("\\s", "") }
    }
}
